{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import Pkg;\n",
    "Pkg.activate(@__DIR__);\n",
    "Pkg.instantiate()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Add packages - uncomment for first-time setup\n",
    "# using Pkg; Pkg.add([\"SparseArrays\", \"OSQP\"])\n",
    "\n",
    "using SparseArrays, OSQP\n",
    "\n",
    "# Utility function\n",
    "speye(N) = spdiagm(ones(N))\n",
    "\n",
    "# Discrete time model of a quadcopter\n",
    "Ad = [1       0       0   0   0   0   0.1     0       0    0       0       0;\n",
    "      0       1       0   0   0   0   0       0.1     0    0       0       0;\n",
    "      0       0       1   0   0   0   0       0       0.1  0       0       0;\n",
    "      0.0488  0       0   1   0   0   0.0016  0       0    0.0992  0       0;\n",
    "      0      -0.0488  0   0   1   0   0      -0.0016  0    0       0.0992  0;\n",
    "      0       0       0   0   0   1   0       0       0    0       0       0.0992;\n",
    "      0       0       0   0   0   0   1       0       0    0       0       0;\n",
    "      0       0       0   0   0   0   0       1       0    0       0       0;\n",
    "      0       0       0   0   0   0   0       0       1    0       0       0;\n",
    "      0.9734  0       0   0   0   0   0.0488  0       0    0.9846  0       0;\n",
    "      0      -0.9734  0   0   0   0   0      -0.0488  0    0       0.9846  0;\n",
    "      0       0       0   0   0   0   0       0       0    0       0       0.9846] |> sparse\n",
    "Bd = [0      -0.0726  0       0.0726;\n",
    "     -0.0726  0       0.0726  0;\n",
    "     -0.0152  0.0152 -0.0152  0.0152;\n",
    "      0      -0.0006 -0.0000  0.0006;\n",
    "      0.0006  0      -0.0006  0;\n",
    "      0.0106  0.0106  0.0106  0.0106;\n",
    "      0      -1.4512  0       1.4512;\n",
    "     -1.4512  0       1.4512  0;\n",
    "     -0.3049  0.3049 -0.3049  0.3049;\n",
    "      0      -0.0236  0       0.0236;\n",
    "      0.0236  0      -0.0236  0;\n",
    "      0.2107  0.2107  0.2107  0.2107] |> sparse\n",
    "(nx, nu) = size(Bd)\n",
    "\n",
    "# Constraints\n",
    "u0 = 10.5916\n",
    "umin = [9.6, 9.6, 9.6, 9.6] .- u0\n",
    "umax = [13, 13, 13, 13] .- u0\n",
    "xmin = [[-pi/6, -pi/6, -Inf, -Inf, -Inf, -1]; -Inf .* ones(6)]\n",
    "xmax = [[pi/6,  pi/6,  Inf,  Inf,  Inf, Inf]; Inf .* ones(6)]\n",
    "\n",
    "# Objective function\n",
    "Q = spdiagm([0, 0, 10, 10, 10, 10, 0, 0, 0, 5, 5, 5])\n",
    "QN = Q\n",
    "R = 0.1 * speye(nu)\n",
    "\n",
    "# Initial and reference states\n",
    "x0 = zeros(12)\n",
    "xr = [0, 0, 1, 0, 0, 0, 0, 0, 0, 0, 0, 0]\n",
    "\n",
    "# Prediction horizon\n",
    "N = 10\n",
    "\n",
    "# Cast MPC problem to a QP: x = (x(0),x(1),...,x(N),u(0),...,u(N-1))\n",
    "# - quadratic objective\n",
    "P = blockdiag(kron(speye(N), Q), QN, kron(speye(N), R))\n",
    "# - linear objective\n",
    "q = [repeat(-Q * xr, N); -QN * xr; zeros(N*nu)]\n",
    "# - linear dynamics\n",
    "Ax = kron(speye(N + 1), -speye(nx)) + kron(spdiagm(-1 => ones(N)), Ad)\n",
    "Bu = kron([spzeros(1, N); speye(N)], Bd)\n",
    "Aeq = [Ax Bu]\n",
    "leq = [-x0; zeros(N * nx)]\n",
    "ueq = leq\n",
    "# - input and state constraints\n",
    "Aineq = speye((N + 1) * nx + N * nu)\n",
    "lineq = [repeat(xmin, N + 1); repeat(umin, N)]\n",
    "uineq = [repeat(xmax, N + 1); repeat(umax, N)]\n",
    "# - OSQP constraints\n",
    "A, l, u = [Aeq; Aineq], [leq; lineq], [ueq; uineq]\n",
    "\n",
    "# Create an OSQP model\n",
    "m = OSQP.Model()\n",
    "\n",
    "# Setup workspace\n",
    "OSQP.setup!(m; P=P, q=q, A=A, l=l, u=u, warm_start=true)\n",
    "\n",
    "# Simulate in closed loop\n",
    "nsim = 15;\n",
    "@time for _ in 1 : nsim\n",
    "    # Solve\n",
    "    res = OSQP.solve!(m)\n",
    "\n",
    "    # Check solver status\n",
    "    if res.info.status != :Solved\n",
    "        error(\"OSQP did not solve the problem!\")\n",
    "    end\n",
    "\n",
    "    # Apply first control input to the plant\n",
    "    ctrl = res.x[(N+1)*nx+1:(N+1)*nx+nu]\n",
    "    global x0 = Ad * x0 + Bd * ctrl\n",
    "\n",
    "    # Update initial state\n",
    "    l[1:nx], u[1:nx] = -x0, -x0\n",
    "    OSQP.update!(m; l=l, u=u)\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "language_info": {
   "name": "python"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
